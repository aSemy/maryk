plugins {
    id "com.moowork.node" version "1.2.0"
}

apply {
    plugin("kotlin-platform-js")
    plugin("com.moowork.node")
}

dependencies {
    expectedBy(project(":core-common"))
    compile(project(":lib-js"))
    compile(project(":yaml-js"))

    testCompile(project(":test-js"))
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "commonjs"
    kotlinOptions.sourceMap = true
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from(zipTree(it.absolutePath).matching { include '*.js' })
    }

    into("${buildDir}/node_modules")
}

node {
    download = true
}

task installMocha(type: NpmTask) {
    args = ['install', 'mocha']
}

task prepareForMocha(dependsOn: [compileTestKotlin2Js, populateNodeModules])

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
    script = file('node_modules/.bin/mocha')
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha
