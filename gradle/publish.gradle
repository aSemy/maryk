apply plugin: 'maven'
apply plugin: 'maven-publish'

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "https://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jurmous"
            name "Jurriaan Mous"
        }
    }

    scm {
        url "https://github.com/marykdb/maryk"
    }
}

project.ext.configureMavenCentralMetadata = {
    def root = it.asNode()
    root.appendNode('name', project.name)
    root.appendNode('description', 'Maryk')
    root.appendNode('url', 'https://github.com/marykdb/maryk')
    root.children().last() + pomConfig
}

publishing {
    repositories {
        maven {
            def user = 'maryk'
            def repo = "maven"
            def name = "maryk-${project.name}"
            url = "https://api.bintray.com/maven/$user/$repo/$name/;publish=0"

            credentials {
                username = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
                password = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            }
        }
    }

    publications.all {
        pom.withXml(configureMavenCentralMetadata)
    }
}
